#!/usr/bin/env simrun

  Copyright (C) 2020 by
  Dipl.-Ing. Michael Niederle

  This program is free software; you can redistribute it and/or modify
  it under the terms of the GNU General Public License, version 2, or
  (at your option) version 3.

  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
  GNU General Public License for more details.

  For details of the GNU General Public License see the accompanying
  files GPLv2.txt and GLPv3.txt or
  http://www.gnu.org/licenses/gpl-2.0.html
  http://www.gnu.org/licenses/gpl-3.0.html
  or write to the
  Free Software Foundation, Inc.,
  51 Franklin Street, Fifth Floor, Boston, MA 02110-1301, USA.

#
  This is a very basic implementation of a scramble like video game to be played
  on a character console.

  https://en.wikipedia.org/wiki/Scramble_(video_game)

  This is by purpose only a skeleton of the game - waiting for *you* to develop
  it into a full fledged action game!

# dynamic content types

$CANNON .
$SHOT .
$LASER_BEAM .

$type_of (!) # used for dynamic contents
$symbol_of (!) # used for dynamic contents
$colour_of (!) # used for dynamic contents
$frequence_of (!) # used for dynamic contents
$tick_of (!) # used for dynamic contents
$x_of (!) # used for dynamic contents
$y_of (!) # used for dynamic contents
$dx_of (!) # used for dynamic contents
$dy_of (!) # used for dynamic contents
$y1_of (!) # used for dynamic contents
$y2_of (!) # used for dynamic contents

$WIDTH 80 # the width of the cave in character columns
$HEIGHT 25 # the height of the cave in character rows
$CAVE_HEIGHT 2*HEIGHT # the height of the cave in half character rows

#
  The height of a character is approximately about twice the size of its width.

  So we use half-blocks to accommodate for this inbalance.

$full_block '@0x2588;'
$upper_half_block '@0x2580;'
$lower_half_block '@0x2584;'
$spaceship_head '@0x2326;'
$spaceship_tail '@0x2327;'
$cannon '@0x25e2;'
$star '@0x2726;'
$mine '@0x2738;'
$shot '\'
$laser_beam '@0x2502;'
$laser_beamer '@0x2551;'

$BLACK     '@0x000000;'
$WHITE     '@0xffffff;'
$GREY      '@0x808080;'
$DARK_GREY '@0x202020;'
$RED       '@0xff0000;'
$GREEN     '@0x00ff00;'
$BLUE      '@0x0000ff;'
$YELLOW    '@0xffff00;'
$ORANGE    '@0xff8000;'
$SKY_BLUE   '@0xc0c0ff;'

$spaceship_colour BLUE
$fuel_tank_colour BLUE
$fuel_tank_text_colour WHITE
$star_colour GREEN
$mine_colour DARK_GREY
$beam_colour YELLOW
$beam_cannon_colour ORANGE
$cannon_colour RED

$$dynamic_contents empty_list

$draw_vertical_bar:
  #
    draws a vertical stack of block graphics characters

    *x* is the character column where to draw the bar

    *top_y* and *bottom_y* are measured in half rows!
  (
    x
    top_y
    bottom_y
  )
  $$y (top_y+1) >> 1 # in rows
  if top_y.is_even:
    set_text_colour_at x y BLACK
    set_character_at x y lower_half_block
    inc &y
  repeat (bottom_y >> 1) - (top_y >> 1):
    set_text_colour_at x y BLACK
    set_character_at x y full_block
    inc &y
  if bottom_y.is_odd:
    set_text_colour_at x y BLACK
    set_character_at x y upper_half_block

$$rng mersenne_twister(4711)
  #
    our pseudo random number generator

    To change the cave just change the seed value.

# initialize the ceiling and floor coordinates of the cave

$$ceiling_y next(&rng 20)+1
  #
    the height of the cave's ceiling - initialized with a random value

$$floor_y CAVE_HEIGHT+1-next(&rng 21)
  #
    the height of the cave's floor - initialized with a random value

$$spaceship_x 1
  #
    the horizontal position (column) of the spaceship

    We start in the leftmost column of the screen.

$$spaceship_y 13
  #
    the vertical position (row) of the spaceship

    We start in the middle of the screen.

$$cave_length 600
  #
    the total length of the cave

$$cave_draw_x 0
  #
    the current drawing position of the cave

$$cave_start_x 0
  #
    the current display start position of the cave

$$fuel 200

$$prev_floor_y undefined
$$prev_ceiling_y undefined
$$next_fuel_tank_x 50

$draw_cave_column:
  #
    draws a new column of the cave
  (
    x # the column number on the screen
  )
  inc &cave_draw_x

  draw_vertical_bar x 1 ceiling_y # draw the ceiling
  draw_vertical_bar x floor_y CAVE_HEIGHT # draw the floor

  # add dynamic content

  $y1 (ceiling_y+1 .div. 2) + 1
  $y2 (floor_y-1 .div. 2)

  cond
    # fuels tanks
    ->
      &&
	cave_draw_x >= next_fuel_tank_x
	prev_ceiling_y < ceiling_y
	next(&rng 7) == 1
      :
	set_text_colour_at x y1-1 BLACK
	set_character_at x y1-1 full_block
	set_background_colour_at x y1 fuel_tank_colour
	set_text_colour_at x y1 fuel_tank_text_colour
	set_character_at x y1 'F'
	!next_fuel_tank_x cave_draw_x+20
	minus &ceiling_y 3
	extend_to &ceiling_y 2

    # laser beams
    -> cave_draw_x > 400 && cave_draw_x <= 500 && next(&rng 40) == 1:
      set_text_colour_at x y1-1 beam_cannon_colour
      set_character_at x y1-1 laser_beamer

      set_text_colour_at x y2+1 beam_cannon_colour
      set_character_at x y2+1 laser_beamer

      push &dynamic_contents
	types::object
	  .type_of LASER_BEAM
	  .x_of cave_draw_x
	  .y1_of y1
	  .y2_of y2

    # cannons
    -> cave_draw_x > 300 && next(&rng 20) == 14 && prev_floor_y >= floor_y:
      set_text_colour_at x y2+1 cannon_colour
      set_character_at x y2+1 cannon

      push &dynamic_contents
	types::object
	  .type_of CANNON
	  .x_of cave_draw_x-1
	  .y_of y2
	  .dx_of -1
	  .dy_of -1
	  .symbol_of shot
	  .colour_of YELLOW
	  .frequence_of
	    if
	      cave_draw_x > 500
	      -> 10
	      -> 20
	  .tick_of 0

    # mines
    -> cave_draw_x > 200 && next(&rng 12) == 1:
      $mine_y next(&rng y1..y2)
      set_text_colour_at x mine_y mine_colour
      set_character_at x mine_y mine

    # stars
    -> cave_draw_x > 100 && next(&rng 12) == 1:
      $star_y next(&rng y1..y2)
      set_text_colour_at x star_y star_colour
      set_character_at x star_y star

  !prev_ceiling_y ceiling_y
  !prev_floor_y floor_y

  # update the ceiling height

  !ceiling_y ceiling_y+next(&rng 5)-3 # make a random change
  extend_to &ceiling_y 2 # not too high
  reduce_to &ceiling_y 35 # not too low

  # update the floor height

  !floor_y floor_y+next(&rng 5)-3 # make a random change
  reduce_to &floor_y CAVE_HEIGHT # not too low
  extend_to &floor_y 16 # not too high

  # check whether the cave would be too narrow

  $cave_height floor_y-ceiling_y # the height of the cave
  if cave_height < 15: # too narrow?
    $delta (15-cave_height) .div. 2 # how much?
    minus &ceiling_y delta # distribute the change to ceiling ...
    plus &floor_y delta # ... and floor

$draw_fuel:
  at 1 1
  set_background_colour BLACK
  if
    fuel > 0:
      set_text_colour WHITE
      draw_text "Fuel: @(pad_left(fuel 4))"
    :
      set_text_colour RED
      draw_text "Out of fuel!"

$draw_dynamic_contents:
  for_each dynamic_contents: (item)
    $x x_of(item)-cave_start_x
    case type_of(item)
      LASER_BEAM:
	if (cave_start_x .mod. 20) >= 15:
	  from_to y1_of(item) y2_of(item): (y)
	    set_text_colour_at x y beam_colour
	    set_character_at x y laser_beam
      SHOT:
	$y y_of(item)
	set_text_colour_at x y colour_of(item)
	set_character_at x y symbol_of(item)

$undraw_dynamic_contents:
  for_each dynamic_contents: (item)
    $x x_of(item)-cave_start_x
    case type_of(item)
      LASER_BEAM:
	if (cave_start_x .mod. 20) >= 15:
	  from_to y1_of(item) y2_of(item): (y)
	    set_character_at x y ' '
      SHOT:
	$y y_of(item)
	set_character_at x y ' '

$update_dynamic_contents:
  update_each &dynamic_contents: (&item)
    if
      x_of(item) <= cave_start_x:
	!item undefined # the item is no longer visible - delete it
      :
	case type_of(item)
	  CANNON:
	    inc &item.tick_of
	    if tick_of(item) == frequence_of(item):
	      !item
		sequence
		  item(.tick_of 0)
		  item(.type_of SHOT)
	  SHOT:
	    $new_x x_of(item)+dx_of(item)
	    $new_y y_of(item)+dy_of(item)
	    if
	      new_x > cave_start_x && character_at(new_x-cave_start_x new_y) == ' ':
		!item.x_of new_x
		!item.y_of new_y
	      :
		!item undefined # crashed into an obstacle

$$original_background_colours undefined

$draw_spaceship
  #
    draws the spaceship
  :
    set_text_colour_at spaceship_x spaceship_y spaceship_colour
    set_text_colour_at spaceship_x+1 spaceship_y spaceship_colour
    set_character_at spaceship_x spaceship_y spaceship_tail
    set_character_at spaceship_x+1 spaceship_y spaceship_head

    # draw cone of light

    $RX 20
    $RY 5

    $cx spaceship_x
    $cy spaceship_y

    $min_x cx
    $max_x min(cx+RX WIDTH)
    $min_y max(cy-RY 1)
    $max_y min(cy+RY HEIGHT)

    !original_background_colours empty_list
    from_to min_y max_y: (y)
      $$background_colour_line empty_list
      from_to min_x max_x: (x)
	$background_colour background_colour_at(x y)
	push &background_colour_line background_colour
	$dx x-cx
	$dy 2*(y-cy)
	if dx >= 2*abs(dy):
	  $distance min(sqrt(dx*dx+dy*dy) RX)
	  $lighting_colour
	    colour_mixture
	      GREY = RX-distance
	      background_colour = distance
	  set_background_colour_at x y lighting_colour
      push &original_background_colours background_colour_line

$undraw_spaceship
  #
    "undraws" the spaceship
  :
    set_character_at spaceship_x spaceship_y ' '
    set_character_at spaceship_x+1 spaceship_y ' '

    $RX 20
    $RY 5

    $cx spaceship_x
    $cy spaceship_y

    $min_x cx
    $max_x min(cx+RX WIDTH)
    $min_y max(cy-RY 1)
    $max_y min(cy+RY HEIGHT)

    from_to min_y max_y: (y)
      $$background_colour_line original_background_colours(y-min_y+1)
      from_to min_x max_x: (x)
	set_background_colour_at x y background_colour_line(x-min_x+1)

$draw_message
  #
    draws a message in the center of the screen
  :
    (
      msg # the message to draw
    )
    $len length_of(msg)
    at div((80-len) 2)+1 13
    set_text_colour BLACK
    set_background_colour WHITE
    draw_text msg

$check_for_collision:
  #
    checks whether the spaceship hit any obstacle

  $check: (x y)
    $chr character_at(x y)
    if
      chr == 'F': # refuel
	plus &fuel 100
      :
	if chr != ' ':
	  $$msg "crashed into the walls of the cave"
	  case chr
	    star: !msg "hit a floating star"
	    mine: !msg "hit a mine"
	    laser_beam: !msg "was hit by a laser beam"
	    laser_beamer: !msg "crashed into a laser beamer"
	    shot: !msg "was hit by the shot of a cannon"
	    cannon: !msg "crashed into a cannon"

	  draw_message "Your ship @(msg)!"
	  update_terminal # make all updates visible
	  sleep 3 # wait for 3 seconds ...
	  terminate # and quit the game

  check spaceship_x+1 spaceship_y
  check spaceship_x spaceship_y

initialize_terminal
  # must be called before any other terminal function

#
  We check for the standard terminal size.

  The cave would be different for other resolutions and results (highscores!)
  could not be compared.

if width_of(current_screen) != 80 || height_of(current_screen) != 25:
  Error "Please change your terminal size to 80x25!"

do: # initialize the terminal
  get_terminal_attributes STDIN_FILENO $$attributes
  use_canonical_mode &attributes false # we want no line-buffering ...
  echo_characters &attributes false # ... and no echoing of typed in characters
  unless set_terminal_attributes(STDIN_FILENO attributes):
    Error "Failed to disable canonical input mode!"
  cursor_off # ... and we want no blinking cursor!

at_exit:
  #
    clean the screen and restore all changes we made to input buffering and
    echoing

  restore_colours
  clear_screen
  cursor_on

  # empty the input queue
  wait_to $event READ_FROM=STDIN_FILENO TIMEOUT=0
  case std::type_of(event)
    READ_FROM: # we got a keystroke
      read_from STDIN_FILENO $dummy

  get_terminal_attributes STDIN_FILENO $$attributes
  use_canonical_mode &attributes false
  echo_characters &attributes true
  unless set_terminal_attributes(STDIN_FILENO attributes):
    Error "Failed to reset input mode!"

# draw welcome screen

do:
  at 10 3
  draw_text "
		        Welcome to the CAVE!

	   (an action game for console/terminal windows)

    Try to survive and escape from the cave into the clear daylight!

    Avoid all obstacles but hit the fuel tanks to refuel your ship!


		   'w' or 'j': move shaceship up

		   's' or 'n': move shaceship down

		   'a' or 'v': move shaceship backwards

		   'd' or 'b': move shaceship forwards

		   'q':        quit the game"


		  Press any key to start the game.

  update_terminal

  wait_to $event READ_FROM=STDIN_FILENO
  read_from STDIN_FILENO $input_buffer

  if input_buffer .has_prefix. 'q' terminate

create_screen !std::current_screen BLACK DARK_GREY

# draw the initial view of the cave

from_to 1 WIDTH: (x) draw_cave_column x # fill all columns of the screen
draw_spaceship # draw the spaceship
update_terminal # make all updates visible
undraw_spaceship

# event loop

$$t current_time()

do: (-> break)
  do_until:
    plus &t 0.08
    $timeout t-current_time()
    wait_to $event READ_FROM=STDIN_FILENO TIMEOUT=timeout
      # we wait for a keystroke or until some time elapsed

    $$input_buffer ""
    case std::type_of(event)
      READ_FROM: # we got a keystroke
	read_from STDIN_FILENO !input_buffer
	sleep t-current_time()

    if
      fuel > 0:
	dec &fuel
      :
	inc &spaceship_y # move the spaceship downwards

    unless input_buffer.is_empty: # are there keys in the backlog buffer?
      case get(&input_buffer)
	'w', 'j':
	  if fuel >= 3:
	    dec &spaceship_y # move the spaceship upwards
	    minus &fuel 3
	's', 'n':
	  if fuel >= 3:
	    inc &spaceship_y # move the spaceship downwards
	    minus &fuel 3
	'a', 'v':
	  if spaceship_x > 1 && fuel >= 3:
	    dec &spaceship_x # move the spaceship backwards
	    minus &fuel 3
	'd', 'b':
	  if spaceship_x < WIDTH && fuel >= 3:
	    inc &spaceship_x # move the spaceship forwards
	    minus &fuel 3
	'q': break # the user wants to quit the game

    # update the view of the cave

    inc &cave_start_x

    $distance_to_exit cave_length-cave_draw_x
    if
      distance_to_exit >= 0:
	if
	  distance_to_exit > 100:
	    set_background_colour DARK_GREY
	  :
	    $a distance_to_exit
	    $b 100-a
	    set_background_colour colour_mixture(DARK_GREY = a SKY_BLUE = b)
      :
	set_background_colour SKY_BLUE
    scroll_left # scroll the whole cave left by 1 character column

    set_background_colour undefined

    if
      cave_draw_x <= cave_length:
	draw_cave_column 80 # draw a new right-most character column
      :
	inc &cave_draw_x

    draw_fuel

    draw_dynamic_contents

    check_for_collision

    draw_spaceship # draw the spaceship

    update_terminal # make all updates visible

    undraw_dynamic_contents
    undraw_spaceship # "undraw" the spaceship

    update_dynamic_contents

    -> cave_draw_x+spaceship_x > cave_length+WIDTH

  draw_message "You survived!"
  update_terminal
  sleep 3
